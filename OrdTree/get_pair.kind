// Retrieves a key_value pair from an AVL tree.
// - K: Key type.
// - V: Value type.
// - ord: Ord instance for K.
// - tree: The AVL tree.
// - key: Key to search for.
// = Maybe containing the key_value pair if found.
OrdTree/get_pair : ∀(K: *) ∀(V: *) ∀(ord: (Ord K)) (OrdTree K V) -> K -> (Maybe (Pair K V))
| K V ord #Leaf key = #None
| K V ord #Node{kv balance left right} key with (Ord/compare K ord key (Pair/get_fst K V kv) )
. | #LT = (OrdTree/get_pair K V ord left key)
. | #EQ = #Some{kv}
. | #GT = (OrdTree/get_pair K V ord right key)

#test:
  let ordtree = (OrdTree/empty U64 U64)
  (OrdTree/get_pair U64 U64 U64/Ord ordtree 2) == #None

#test:
  let ordtree = (OrdTree/empty U64 U64)
  let ordtree = (OrdTree/insert U64 U64 (U64/Ord) ordtree #Pair{2 20})
  (OrdTree/get_pair U64 U64 U64/Ord ordtree 2) == #Some{#Pair{2 20}}

#test:
  let ordtree = (OrdTree/empty U64 U64)
  let ordtree = (OrdTree/insert U64 U64 (U64/Ord) ordtree #Pair{1 20})
  let ordtree = (OrdTree/insert U64 U64 (U64/Ord) ordtree #Pair{3 20})
  let ordtree = (OrdTree/insert U64 U64 (U64/Ord) ordtree #Pair{4 20})
  let ordtree = (OrdTree/insert U64 U64 (U64/Ord) ordtree #Pair{2 20})
  (OrdTree/get_pair U64 U64 U64/Ord ordtree 2) == #Some{#Pair{2 20}}

#test:
  let ordtree = (OrdTree/singleton U64 U64 #Pair{1 1})
  let ordtree = (OrdTree/insert U64 U64 U64/Ord ordtree #Pair{2 2})
  (OrdTree/get_pair U64 U64 U64/Ord ordtree 2) == #Some{#Pair{2 2}}
