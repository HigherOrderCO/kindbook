// Retrieves a value from an OrdTree tree.
// - K: Key type.
// - V: Value type.
// - ord: Ord instance for K.
// - tree: The Ordtree tree.
// - key: Key to search for.
// = Maybe containing the value if found.
OrdTree/get : ∀(K: *) ∀(V: *) ∀(ord: (Ord K)) (OrdTree K V) -> K -> (Maybe V)
| K V ord #Leaf                                        key = #None
| K V ord #Node{#Pair{curr_key curr_val} _ left right} key with (Ord/compare K ord key curr_key)
. | #LT = (OrdTree/get K V ord left key)
. | #EQ = #Some{curr_val}
. | #GT = (OrdTree/get K V ord right key)

#test:
  let ordtree = (OrdTree/empty U64 String)
  (OrdTree/get U64 String U64/Ord ordtree 1) == #None

#test:
  let ordtree = (OrdTree/singleton U64 String #Pair{1 "one"})
  (OrdTree/get U64 String U64/Ord ordtree 1) == #Some{"one"}

#test:
  let ordtree = (OrdTree/singleton U64 String #Pair{1 "one"})
  (OrdTree/get U64 String U64/Ord ordtree 2) == #None

#test:
  let ordtree = (OrdTree/singleton String U64 #Pair{"RightClick" 1})
  let ordtree = (OrdTree/insert String U64 String/Ord ordtree #Pair{"E" 2})
  (OrdTree/get String U64 String/Ord ordtree "E") == #Some{2}

#test:
  let ordtree = (OrdTree/singleton U64 U64 #Pair{1 1})
  let ordtree = (OrdTree/insert U64 U64 U64/Ord ordtree #Pair{2 2})
  (OrdTree/get U64 U64 U64/Ord ordtree 2) == #Some{2}