// Calculates the average of three V2 points.
// - v1: First point.
// - v2: Second point.
// - v3: Third point.
// = The center point (average of the three points).
Shape/center : V2 -> V2 -> V2 -> V2
| #V2{x1 y1} #V2{x2 y2} #V2{x3 y3} = #V2{
    (F64/div (F64/add (F64/add x1 x2) x3) 3.0)
    (F64/div (F64/add (F64/add y1 y2) y3) 3.0)
  }

// Creates a triangular Shape from three vertices.
// - v1: First vertex.
// - v2: Second vertex.
// - v3: Third vertex.
// = A Shape representing the triangle.
Shape/triangle : V2 -> V2 -> V2 -> Shape
| v1 v2 v3 =
  let c   = (Shape/center v1 v2 v3)
  let rv1 = (V2/sub v1 c)
  let rv2 = (V2/sub v2 c)
  let rv3 = (V2/sub v3 c)
  #Polygon{
    center: c
    vertices: (#Cons{rv1 #Cons{rv2 #Cons{rv3 #Nil}}})
  }

#test: (Shape/triangle #V2{x: 0.0 y: 0.0} #V2{x: 3.0 y: 0.0} #V2{x: 0.0 y: 3.0}) ==
  #Polygon{
    center: #V2{x: 1.0 y: 1.0}
    vertices: [
      #V2{x: -1.0 y: -1.0}
      #V2{x: 2.0 y: -1.0}
      #V2{x: -1.0 y: 2.0}
    ]
  }
