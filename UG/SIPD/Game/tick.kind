use UG/SIPD as G

// Updates a cooldown in the cooldowns tree.
// - kv: The key-value pair to update.
// - cooldowns: The cooldowns tree.
// = The updated cooldowns tree.
update_cooldown : (Pair String U64) -> (OrdTree String U64) -> (OrdTree String U64)
| #Pair{key value} cooldowns =
  if (U64/to_bool (== value 0)) {
    cooldowns
  } else {
    (OrdTree/insert String U64 String/Ord cooldowns #Pair{key (- value 1)})
  }

// Decrements all cooldowns in a hero state.
// - state: The hero state to update.
// = The updated hero state.
decrement_cooldowns : G/Hero/HeroState -> G/Hero/HeroState
| #HeroState{cooldowns health} =
  let new_cooldowns = (OrdTree/fold String U64 cooldowns (OrdTree String U64) cooldowns update_cooldown)
  #HeroState{new_cooldowns health}

update_hero_state : G/State -> U64 -> G/Hero/HeroState -> G/State
| #State{next_id tick players hero_states game_map} id hs = 
  let new_hero_states = (Map/insert G/Hero/HeroState hero_states id (decrement_cooldowns hs))
  #State{next_id tick players new_hero_states game_map}

// Updates cooldowns for all hero states.
// - state: The game state to update.
// = The updated game state.
update_cooldowns : G/State -> G/State
| #State{next_id tick players hero_states #GameMap{width height bodies bodies_count}} = 
  (Map/fold G/Hero/HeroState G/State hero_states bodies_count #State{next_id tick players hero_states #GameMap{width height bodies bodies_count}} update_hero_state)

UG/SIPD/Body/tick : G/Body -> (G/State -> G/State)
| #Body{id hitbox tick effects collidable side} = tick

// Updates the game state for one tick.
// - state: The current game state.
// = The updated game state after one tick.
UG/SIPD/Game/tick : G/State -> G/State
| #State{next_id tick players hero_states game_map} =
  let #GameMap{width height bodies bodies_count} = game_map
  let body_values = (Map/values G/Body bodies bodies_count)
  let updated_gs = (List/fold G/Body body_values G/State #State{next_id tick players hero_states game_map} λbody λacc ((G/Body/tick body) acc))
  let #State{next_id tick players hero_states new_game_map} = updated_gs
  let updated_tick = (U64/add tick 1)
  (update_cooldowns #State{next_id updated_tick players hero_states new_game_map})

