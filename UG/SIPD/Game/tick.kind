use UG/SIPD as G

// Updates a cooldown in the cooldowns tree.
// - kv: The key-value pair to update.
// - cooldowns: The cooldowns tree.
// = The updated cooldowns tree.
update_cooldown : (Pair String U64) -> (OrdTree String U64) -> (OrdTree String U64)
| #Pair{key value} cooldowns =
  if (U64/to_bool (== value 0)) {
    cooldowns
  } else {
    (OrdTree/insert String U64 String/Ord cooldowns #Pair{key (- value 1)})
  }

// Decrements all cooldowns in a hero state.
// - state: The hero state to update.
// = The updated hero state.
decrement_cooldowns : G/Hero/HeroState -> G/Hero/HeroState
| #HeroState{cooldowns} =
  let new_cooldowns = (OrdTree/fold String U64 cooldowns (OrdTree String U64) cooldowns update_cooldown)
  #HeroState{new_cooldowns}

update_hero_state : (Pair U64 G/Hero/HeroState) -> G/State -> G/State
| #Pair{id hs} #State{next_id tick players hero_states game_map} = 
  let new_hero_states = (OrdTree/insert U64 G/Hero/HeroState U64/Ord hero_states #Pair{id (decrement_cooldowns hs)})
  #State{next_id tick players new_hero_states game_map}

// Updates cooldowns for all hero states.
// - state: The game state to update.
// = The updated game state.
update_cooldowns : G/State -> G/State
| #State{next_id tick players hero_states game_map} = 
  (OrdTree/fold U64 G/Hero/HeroState hero_states G/State #State{next_id tick players hero_states game_map} update_hero_state)

UG/SIPD/Body/tick : G/Body -> (G/State -> G/State)
| #Body{id hitbox tick effects collidable} = tick

// Updates the game state for one tick.
// - state: The current game state.
// = The updated game state after one tick.
UG/SIPD/Game/tick : G/State -> G/State
| #State{next_id tick players hero_states game_map} =
  let #GameMap{width height bodies} = game_map
  let body_values = (OrdTree/values U64 G/Body bodies)
  let updated_gs = (List/fold G/Body body_values G/State #State{next_id tick players hero_states game_map} λbody λacc ((G/Body/tick body) acc))
  let #State{next_id tick players hero_states game_map} = updated_gs
  let updated_tick = (U64/add tick 1)
  (update_cooldowns #State{next_id updated_tick players hero_states game_map})

