// Applies a monadic function to each list element, threading a state.
// - M: Monad type constructor.
// - m: Monad instance.
// - A: Element type.
// - S: State type.
// - x: Input list.
// - s: Initial state.
// - f: Monadic function (state -> element -> M state).
// = Final state after processing all elements.
List/for
: ∀(M: ∀(A: *) *)
  ∀(m: (Monad M))
  ∀(A: *)
  ∀(S: *)
  ∀(x: (List A))
  ∀(s: S)
  ∀(f: ∀(s: S) ∀(a: A) (M S))
  (M S)
| M #Monad{pure bind} A S #Nil{}           s f = (pure S s)
| M #Monad{pure bind} A S #Cons{head tail} s f = (bind S S (f s head) (λx (List/for M #Monad{pure bind} A S tail x f)))
