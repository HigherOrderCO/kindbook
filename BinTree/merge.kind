// Merges two trees using combining functions.
// - A: Type of elements in the first tree.
// - B: Type of elements in the second tree.
// - C: Type of elements in the resulting tree.
// - a: The first input tree.
// - b: The second input tree.
// - f: Function for when only the first tree has a value.
// - g: Function for when only the second tree has a value.
// - h: Function to combine values when both trees have a value.
// = A new tree with combined values based on input trees.
BinTree/merge
: ∀(A: *)
  ∀(B: *)
  ∀(C: *)
  ∀(a: (BinTree A))
  ∀(b: (BinTree B))
  ∀(f: A -> C)
  ∀(g: B -> C)
  ∀(h: A -> B -> C)
  (BinTree C)
| A B C #Leaf #Leaf f g h =
  #Leaf
| A B C #Leaf #Node{bx bl br} f g h =
  let cl = (BinTree/merge A B C #Leaf bl f g h)
  let cr = (BinTree/merge A B C #Leaf br f g h)
  #Node{(g bx) cl cr}
| A B C #Node{ax al ar} #Leaf f g h =
  let cl = (BinTree/merge A B C al #Leaf f g h)
  let cr = (BinTree/merge A B C ar #Leaf f g h)
  #Node{(f ax) cl cr}
| A B C #Node{ax al ar} #Node{bx bl br} f g h =
  let cl = (BinTree/merge A B C al bl f g h)
  let cr = (BinTree/merge A B C ar br f g h)
  #Node{(h ax bx) cl cr}
