use Bend/Parser/Fun/parse_def_sig as .
use Bend/Fun/Type as Type
use Bend/Parser as BP

// Parses the type signature of a function definition.
// = The function name, a list of named arguments and the function type.
BP/Fun/parse_def_sig : (BP (String & (List String) & Type))
| = do BP {
  ask has_parens        = (BP/try_consume "(")
  ask _                 = (BP/skp)
  ask (name, args, ret) = if has_parens do BP{
    ask name = (BP/parse_top_level_name)
    ask args = (BP/list_like _ ./parse_arg "" ")" "" #False #False 0)
    ask _    = (BP/skp)
    ask _    = (BP/consume ":")
    ask _    = (BP/skp)
    ask typ  = (BP/Fun/parse_type_term)
    ret (name, args, typ) :: (_ & _ & _)
  } else do BP {
    ask name = (BP/parse_top_level_name)
    ask args = (BP/list_like _ ./parse_arg "" ":" "" #False #False 0)
    ask _    = (BP/skp)
    ask typ  = (BP/Fun/parse_type_term)
    ret (name, args, typ) :: (_ & _ & _)
  }
  let (args, arg_types) = (List/unzip _ _ args)
  let typ = (List/foldr _ arg_types _ ret (λarg λacc #Arr{arg acc}))
  ret (name, args, typ) :: (_ & _ & _)
}

// Parses a single argument in a function signature.
// = The argument name and its type (Any if no type is specified).
./parse_arg : (BP (Pair String Type))
| = do BP {
  ask has_parens = (BP/try_consume "(")
  ask _          = (BP/skp)
  if has_parens {
    do BP {
      ask nam = (BP/parse_name "function argument")
      ask _   = (BP/skp)
      ask typ = (BP/first_with_guard _ 
                  [((BP/try_consume ":"), (BP/Fun/parse_type_term))]
                  (BP/pure Type #Any))
      ask _   = (BP/consume ")")
      ret (nam, typ) :: (_ & _)
    }
  } else {
    do BP {
      ask nam = (BP/parse_name "function argument")
      ret (nam, #Any) :: (_ & Type)
    }
  }
}
