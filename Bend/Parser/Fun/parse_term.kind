use Bend/Parser/Fun/parse_term as .
use Bend/Fun/Term as Term
use Bend/Fun/FnDef as FnDef
use Bend/Fun/MatchRule as MatchRule
use Bend/Parser as BP

// Parses a Fun syntax term.
// - parse_fn_def: A parser for function definitions.
// = The parsed term.
BP/Fun/parse_term : (BP Term)
| = do BP {
  let starts_with_lam = do BP {
    ask is_lam = (BP/starts_with "λ")
    ask is_at  = (BP/starts_with "@")
    ret (Bool/or is_lam is_at)
  }
  let starts_with_num = do BP {
    ask x = (BP/peek_one)
    ret (Maybe/fold _ x Bool #False BP/is_num_char)
  }
  let starts_with_nam = do BP {
    ask x = (BP/peek_one)
    ret (Maybe/fold _ x Bool #False BP/is_name_char)
  }
  
  ask _ = (BP/skp)
  (BP/first_with_guard _ [
    (starts_with_lam                  , ./parse_lam)
    ((BP/starts_with_keyword "let")   , ./parse_let)
    ((BP/starts_with_keyword "use")   , ./parse_use)
    ((BP/starts_with_keyword "ask")   , ./parse_ask)
    ((BP/starts_with_keyword "match") , ./parse_mat)
    ((BP/starts_with_keyword "switch"), ./parse_swi)
    ((BP/starts_with_keyword "fold")  , ./parse_fld)
    ((BP/starts_with_keyword "bend")  , ./parse_bnd)
    ((BP/starts_with_keyword "open")  , ./parse_opn)
    ((BP/starts_with_keyword "def")   , ./parse_def)
    ((BP/starts_with_keyword "with")  , ./parse_wth)
    ((BP/starts_with "(")         , ./parse_parens)
    ((BP/starts_with "{")         , ./parse_sup)
    ((BP/starts_with "[")         , ./parse_lst)
    ((BP/starts_with "\"")        , ./parse_str)
    ((BP/starts_with "'")         , ./parse_chr)
    ((BP/starts_with "`")         , ./parse_sym)
    ((BP/starts_with "$")         , ./parse_lnk)
    ((BP/starts_with "*")         , ./parse_era)
    ((BP/starts_with "![")        , ./parse_tree_node)
    ((BP/starts_with "!")         , ./parse_tree_leaf)
    (starts_with_num                  , ./parse_num)
    (starts_with_nam                  , ./parse_var)
  ] (BP/fail _ "Expected term"))
}

./parse_name_or_era : (BP (Maybe String))
| = do BP {
  ask is_era = (BP/starts_with "*")
  if is_era do BP {
    ask _ = BP/advance_one
    ret #None :: (Maybe _)
  } else do BP {
    ask nam = BP/parse_var_name
    ret #Some{nam} :: (Maybe _)
  }
}

./parse_lam : (BP Term)
| = do BP {
  ask is_lam = (BP/starts_with "λ")
  ask is_at  = (BP/starts_with "@")
  if (Bool/or is_lam is_at) do BP {
    ask _   = BP/advance_one
    ask pat = BP/Fun/parse_pattern
    ask bod = BP/Fun/parse_term
    ret #Lam{pat bod} :: Term
  } else {
    (BP/fail _"Expected 'λ' or '@'")
  }
}

./parse_parens : (BP Term)
| = do BP {
  ask _      = (BP/consume "(")
  ask _      = (BP/skp)
  ask opr    = (BP/alternative _
    (do BP {ask opr = BP/parse_oper ret #Some{opr} :: (Maybe _)})
    (do BP {ret #None}))
  ask _      = (BP/skp)
  ask is_tup = (BP/try_consume ",")
  match opr {
    #Some{opr}: if is_tup {
      match opr {
        #Mul: do BP {
          ask (BP/skp)
          (./parse_tup #Era)
        }
        _   : (BP/fail _ "Expected term")
      }
    } else do BP {
      ask fst = (BP/Fun/parse_term)
      ask _   = (BP/skp)
      ask snd = (BP/Fun/parse_term)
      ask _   = (BP/skp)
      ask _   = (BP/consume ")")
      ret #Opr{opr fst snd} :: Term
    }
    #None: do BP {
      ask head   = (BP/Fun/parse_term)
      ask _      = (BP/skp)
      ask is_tup = (BP/try_consume ",")
      if is_tup do BP {
        ask (BP/skp)
        (./parse_tup head)
      } else {
        (./parse_app head)
      }
    }
  }
}

./parse_tup : Term -> (BP Term)
| head = do BP {
  ask tail = (BP/list_like _ BP/Fun/parse_term "" ")" "," #True #False 1)
  ret #Fan{#Tup (head;;tail)} :: Term
}

./parse_app : Term -> (BP Term)
| head = do BP {
  ask tail = (BP/list_like _ BP/Fun/parse_term "" ")" "" #False #False 0)
  ret (List/foldl _ tail _ head λa λb #App{a b})
}

./parse_sup : (BP Term)
| = do BP {
  ask els = (BP/list_like _ BP/Fun/parse_term "{" "}" "," #False #False 2)
  ret #Fan{#Dup els} :: Term
}

./parse_lst : (BP Term)
| = do BP {
  ask els = (BP/list_like _ BP/Fun/parse_term "[" "]" "," #False #False 0)
  ret #Lst{els} :: Term
}

./parse_str : (BP Term)
| = do BP {
  ask str = BP/parse_quoted_string
  ret #Str{str} :: Term
}

./parse_chr : (BP Term)
| = do BP {
  ask chr = BP/parse_quoted_char
  ret #Num{#U24{chr}} :: Term
}

./parse_sym : (BP Term)
| = do BP {
  ask _   = (BP/consume "`")
  ask sym = (./parse_sym/go 4 0)
  ret #Num{#U24{sym}} :: Term
}
./parse_sym/go : U64 -> U64 -> (BP U64)
| 0   acc = do BP {
  ask (BP/consume "`")
  ret acc
}
| 1+n acc = do BP {
  ask c = (BP/advance_one)
  match c {
    #Some{c}:
      if (Bool/and (Char/gte c 'A') (Char/lte c 'Z')) {
        (./parse_sym/go n (+ (<< acc 6) (- c 'A')))
      } else if (Bool/and (Char/gte c 'a') (Char/lte c 'z')) {
        (./parse_sym/go n (+ (<< acc 6) (+ (- c 'A') 26)))
      } else if (Bool/and (Char/gte c '0') (Char/lte c '9')) {
        (./parse_sym/go n (+ (<< acc 6) (+ (- c '0') 52)))
      } else if (Char/eq c '+') {
        (./parse_sym/go n (+ (<< acc 6) 62))
      } else if (Char/eq c '/')  {
        (./parse_sym/go n (+ (<< acc 6) 63))
      } else if (Char/eq c '`') {
        (BP/pure _ acc)
      } else {
        (BP/fail _ (String/concat ["Invalid symbol character '" (String/from_char c) "'"]))
      }
    #None: (BP/fail _ "Expected symbol")
  }
}

./parse_let : (BP Term)
| = do BP {
  ask _   = (BP/parse_keyword "let")
  ask _   = (BP/skp)
  ask pat = (BP/Fun/parse_pattern)
  ask _   = (BP/skp)
  ask _   = (BP/consume "=")
  ask _   = (BP/skp)
  ask val = (BP/Fun/parse_term)
  ask _   = (BP/skp)
  ask _   = (BP/try_consume ";")
  ask _   = (BP/skp)
  ask nxt = (BP/Fun/parse_term)
  ret #Let{pat val nxt} :: Term
}

./parse_use : (BP Term)
| = do BP {
  ask _   = (BP/parse_keyword "use")
  ask _   = (BP/skp)
  ask nam = (BP/parse_var_name)
  ask _   = (BP/skp)
  ask _   = (BP/consume "=")
  ask _   = (BP/skp)
  ask val = (BP/Fun/parse_term)
  ask _   = (BP/skp)
  ask _   = (BP/try_consume ";")
  ask _   = (BP/skp)
  ask nxt = (BP/Fun/parse_term)
  ret #Use{#Some{nam} val nxt} :: Term
}

./parse_ask : (BP Term)
| = do BP {
  ask _   = (BP/parse_keyword "ask")
  ask _   = (BP/skp)
  ask pat = (BP/Fun/parse_pattern)
  ask _   = (BP/skp)
  ask _   = (BP/consume "=")
  ask _   = (BP/skp)
  ask val = BP/Fun/parse_term
  ask _   = (BP/skp)
  ask _   = (BP/try_consume ";")
  ask _   = (BP/skp)
  ask nxt = BP/Fun/parse_term
  ret #Ask{pat val nxt} :: Term
}

./parse_wth: (BP Term)
| = do BP {
  ask _   = (BP/parse_keyword "with")
  ask _   = (BP/skp)
  ask typ = (BP/parse_name "type")
  ask _   = (BP/skp)
  ask _   = (BP/consume "{")
  ask _   = (BP/skp)
  ask bod = BP/Fun/parse_term
  ask _   = (BP/skp)
  ask _   = (BP/try_consume "}")
  ask _   = (BP/skp)
  ret #Wth{typ bod} :: Term
}

./parse_mat: (BP Term)
| = do BP {
  ask _            = (BP/parse_keyword "match")
  ask _            = (BP/skp)
  ask (bnd, arg)   = (./parse_match_arg)
  ask _            = (BP/skp)
  ask with         = (./parse_match_with)
  ask _            = (BP/skp)
  let (wbnd, warg) = (List/unzip _ _ with)
  ask arms         = (BP/list_like _ ./parse_match_arm "{" "}" ";" #False #False 1)
  ret #Mat{bnd arg wbnd warg arms} :: Term
}

./parse_swi : (BP Term)
| = do BP {
  ask _            = (BP/parse_keyword "switch")
  ask _            = (BP/skp)
  ask (bnd, arg)   = (./parse_match_arg)
  ask _            = (BP/skp)
  ask with         = (./parse_match_with)
  ask _            = (BP/skp)
  let (wbnd, warg) = (List/unzip _ _ with)
  ask _            = (BP/consume "{")
  ask _            = (BP/skp)
  ask (pred, arms) = (./parse_switch_arms 0 [])
  ask _            = (BP/skp)
  ask _            = (BP/consume "}")
  ask _            = (BP/skp)
  let arms         = (List/reverse _ arms)
  let pred         = (Maybe/map _ _ bnd (λbnd (String/concat [bnd "-" (U64/to_string pred)])))
  ret #Swt{bnd arg wbnd warg pred arms} :: Term
}

./parse_named_arg : (BP ((Maybe String) & Term))
| = do BP {
  ask nam     = (BP/parse_name "argument name")
  ask _       = (BP/skp)
  ask has_arg = (BP/try_consume "=")
  if has_arg do BP {
    ask _   = BP/skp
    ask arg = BP/Fun/parse_term
    ret (#Some{nam}, arg) :: ((Maybe _) & Term)
  } else do BP {
    ret (#Some{nam}, #Var{nam}) :: ((Maybe _) & Term)
  }
}

./parse_match_arg : (BP ((Maybe String) & Term))
| = do BP {
  ask arg     = (BP/Fun/parse_term)
  ask _       = (BP/skp)
  ask has_bnd = (BP/try_consume "=")
  if has_bnd {
    match arg {
      // bnd and arg
      #Var{bnd}: do BP {
        ask _   = (BP/skp)
        ask arg = BP/Fun/parse_term
        ret (#Some{bnd}, arg) :: ((Maybe _) & Term)
      }
      // bnd but no arg
      _: (BP/fail _ "Expected argument name")
    }
  } else {
    match arg {
      // no bnd, but arg is var
      #Var{bnd}: (BP/pure ((Maybe _) & Term) (#Some{bnd}, arg))
      // no bnd, just arg
      _        : (BP/pure ((Maybe _) & Term) (#Some{"%arg"}, arg))
    }
  }
}

./parse_match_with : (BP (List (Pair (Maybe String) Term)))
| = do BP {
  ask has = (BP/try_consume "with")
  if has do BP {
    ask BP/skp
    (BP/list_like _ ./parse_named_arg "" "}" "," #False #False 1)
  } else do BP {
    ret [] :: (List _)
  }
}

./parse_match_arm : (BP MatchRule)
| = do BP {
  ask _   = (BP/try_consume "|")
  ask _   = (BP/skp)
  ask nam = (./parse_name_or_era)
  ask _   = (BP/skp)
  ask _   = (BP/consume ":")
  ask _   = (BP/skp)
  ask bod = (BP/Fun/parse_term)
  ret #MatchRule{nam [] bod} :: MatchRule
}

./parse_switch_arms : U64 -> (List Term) -> (BP (Pair U64 (List Term)))
| n acc = do BP {
  ask _    = (BP/try_consume "|")
  ask _    = (BP/skp)
  ask pred = (BP/try_consume "_")
  ask _    = (BP/skp)
  if pred do BP {
    ask _    = (BP/consume ":")
    ask _    = (BP/skp)
    ask term = (BP/Fun/parse_term)
    ask _    = (BP/skp)
    ask _    = (BP/try_consume ";")
    ask _    = (BP/skp)
    (./parse_switch_arms (+ n 1) (term;;acc))
  } else do BP {
    ask num  = (BP/parse_number)
    ask _    = (BP/skp)
    ask _    = (BP/consume ":")
    ask _    = (BP/skp)
    ask term = (BP/Fun/parse_term)
    ask _    = (BP/skp)
    ask _    = (BP/try_consume ";")
    ask _    = (BP/skp)
    (./parse_switch_arms (+ n 1) (term;;acc))
  }
}

./parse_fld : (BP Term)
| = do BP {
  ask _            = (BP/parse_keyword "fold")
  ask _            = (BP/skp)
  ask (bnd, arg)   = (./parse_match_arg)
  ask _            = (BP/skp)
  ask with         = (./parse_match_with)
  ask _            = (BP/skp)
  let (wbnd, warg) = (List/unzip _ _ with)
  ask arms         = (BP/list_like _ ./parse_match_arm "{" "}" ";" #False #False 1)
  ret #Fld{bnd arg wbnd warg arms} :: Term
}

./parse_bnd : (BP Term)
| = do BP {
  ask _          = (BP/parse_keyword "bend")
  ask _          = (BP/skp)
  ask args       = (BP/list_like _ ./parse_named_arg "" "{" "," #False #False 1)
  ask _          = (BP/skp)
  let (bnd, arg) = (List/unzip _ _ args)
  ask _          = (BP/parse_keyword "when")
  ask _          = (BP/skp)
  ask cond       = (BP/Fun/parse_term)
  ask _          = (BP/skp)
  ask _          = (BP/consume ":")
  ask _          = (BP/skp)
  ask step       = (BP/Fun/parse_term)
  ask _          = (BP/skp)
  ask _          = (BP/parse_keyword "else")
  ask _          = (BP/skp)
  ask _          = (BP/consume ":")
  ask _          = (BP/skp)
  ask base       = (BP/Fun/parse_term)
  ask _          = (BP/skp)
  ask _          = (BP/consume "}")
  ret #Bnd{bnd arg cond step base} :: Term
}

./parse_opn : (BP Term)
| = do BP {
  ask _   = (BP/parse_keyword "open")
  ask _   = (BP/skp)
  ask typ = (BP/parse_name "type")
  ask _   = (BP/skp)
  ask var = (BP/parse_var_name)
  ask _   = (BP/skp)
  ask _   = (BP/try_consume ";")
  ask _   = (BP/skp)
  ask bod = (BP/Fun/parse_term)
  ret #Opn{typ #Some{var} [] bod} :: Term
}

./parse_tree_node : (BP Term)
| = do BP {
  ask _   = (BP/consume "![")
  ask _   = (BP/skp)
  ask lft = (BP/Fun/parse_term)
  ask _   = (BP/skp)
  ask rgt = (BP/Fun/parse_term)
  ask _   = (BP/skp)
  ask _   = (BP/consume "]")
  ret #App{#App{#Ref{"Tree/Node"} lft} rgt} :: Term
}

./parse_tree_leaf : (BP Term)
| = do BP {
  ask _   = (BP/consume "!")
  ask _   = (BP/skp)
  ask val = (BP/Fun/parse_term)
  ret #App{#Ref{"Tree/Leaf"} val} :: Term
}

./parse_def : (BP Term)
| = do BP {
  ask _   = (BP/parse_keyword "def")
  ask _   = (BP/skp)
  ask def = (BP/Fun/parse_fn_def)
  ask _   = (BP/skp)
  ask nxt = (BP/Fun/parse_term)
  ret #Def{def nxt} :: Term
}

./parse_lnk : (BP Term)
| = do BP {
  ask _   = (BP/consume "$")
  ask _   = (BP/skp)
  ask nam = (BP/parse_name "unscoped variable")
  ret #Lnk{nam} :: Term
}

./parse_era : (BP Term)
| = do BP {
  ask _ = (BP/consume "*")
  ret #Era :: Term
}

./parse_num : (BP Term)
| = do BP {
  ask num = (BP/parse_number)
  ret #Num{num} :: Term
}

./parse_var : (BP Term)
| = do BP {
  ask nam = (BP/parse_var_name)
  ret #Var{nam} :: Term
}
