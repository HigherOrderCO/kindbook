use Bend/Parser/Imp/parse_expr as .
use Bend/Imp/Expr as Expr
use Bend/Fun/Op as Op
use Bend/Parser as BP

BP/Imp/parse_expr : Bool -> Bool -> (BP Expr)
| inline tup = do BP {
  ask (BP/skp_inline inline)
  ask term = (./parse_infix 0 inline)
  if tup {
    (./parse_tup inline tup term)
  } else{
    (BP/pure _ term)
  }
}

./parse_tup : Bool -> Bool -> Expr -> (BP Expr)
| inline tup head = do BP {
  ask (BP/skp_inline inline)
  ask cma = (BP/try_consume ",")
  if cma do BP {
    ask (BP/skp_inline inline)
    ask end1 = (BP/starts_with ")")
    ask end2 = (BP/starts_with ":")
    if (Bool/or end1 end2) do BP {
      ret head
    } else do BP {
      ask tail = (BP/Imp/parse_expr inline tup)
      match tail {
        #Tup{tail}: (BP/pure _ #Tup{head ;; tail})
        _         : (BP/pure _ #Tup{[head tail]})
      }
    }
  } else {
    (BP/pure _ head)
  }
}

./max_precedence : U64
| = 8

./parse_infix : U64 -> Bool -> (BP Expr)
| prec inline =
  if (U64/gt prec ./max_precedence) {
    (./parse_single inline)
  } else do BP {
    ask lhs = (./parse_infix (+ 1 prec) inline)
    ask _   = (BP/skp_inline inline)
    (./parse_infix/rhs lhs prec inline)
  }
./parse_infix/rhs : Expr -> U64 -> Bool -> (BP Expr)
| lhs prec inline = do BP {
  ask op  = (./peek_op)
  match op {
    #Some{op}:
      if (U64/eq (./prec op) prec) do BP {
        ask _   = (./advance_op op)
        ask rhs = (./parse_infix (+ 1 prec) inline)
        ask _   = (BP/skp_inline inline)
        let lhs = #Opr{op lhs rhs} :: Expr
        (./parse_infix/rhs lhs prec inline)
      } else {
        (BP/pure _ lhs)
      }
    #None: (BP/pure _ lhs)
  }
}

./prec : Op -> U64
| #Or  = 0
| #Xor = 1
| #And = 2
| #Eql = 3
| #Neq = 3
| #Ltn = 4
| #Gtn = 4
| #Lte = 4
| #Gte = 4
| #Shl = 5
| #Shr = 5
| #Add = 6
| #Sub = 6
| #Mul = 7
| #Div = 7
| #Rem = 7
| #Pow = 8

./peek_op : (BP (Maybe Op))
| = (BP/first_with_guard _ [
  ((BP/starts_with "+") , (BP/pure _ #Some{#Add}))
  ((BP/starts_with "-") , (BP/pure _ #Some{#Sub}))
  ((BP/starts_with "*") , (BP/pure _ #Some{#Mul}))
  ((BP/starts_with "/") , (BP/pure _ #Some{#Div}))
  ((BP/starts_with "%") , (BP/pure _ #Some{#Rem}))
  ((BP/starts_with "=="), (BP/pure _ #Some{#Eql}))
  ((BP/starts_with "!="), (BP/pure _ #Some{#Neq}))
  ((BP/starts_with "<<"), (BP/pure _ #Some{#Shl}))
  ((BP/starts_with ">>"), (BP/pure _ #Some{#Shr}))
  ((BP/starts_with "<="), (BP/pure _ #Some{#Lte}))
  ((BP/starts_with ">="), (BP/pure _ #Some{#Gte}))
  ((BP/starts_with "<") , (BP/pure _ #Some{#Ltn}))
  ((BP/starts_with ">") , (BP/pure _ #Some{#Gtn}))
  ((BP/starts_with "&") , (BP/pure _ #Some{#And}))
  ((BP/starts_with "|") , (BP/pure _ #Some{#Or}))
  ((BP/starts_with "^") , (BP/pure _ #Some{#Xor}))
  ((BP/starts_with "**"), (BP/pure _ #Some{#Pow}))
] (BP/pure _ #None))

./advance_op : Op -> (BP (Maybe String))
| #Or  = (BP/advance_many 1)
| #Xor = (BP/advance_many 1)
| #And = (BP/advance_many 1)
| #Eql = (BP/advance_many 2)
| #Neq = (BP/advance_many 2)
| #Ltn = (BP/advance_many 1)
| #Gtn = (BP/advance_many 1)
| #Lte = (BP/advance_many 2)
| #Gte = (BP/advance_many 2)
| #Shl = (BP/advance_many 2)
| #Shr = (BP/advance_many 2)
| #Add = (BP/advance_many 1)
| #Sub = (BP/advance_many 1)
| #Mul = (BP/advance_many 1)
| #Div = (BP/advance_many 1)
| #Rem = (BP/advance_many 1)
| #Pow = (BP/advance_many 2)

// Parses a term without any infix operators, commas, etc.
./parse_single : Bool -> (BP Expr)
| inline = do BP {
  ask term = (./parse_primary inline)
  ask (BP/skp_inline inline)
  (./parse_postfix term inline)
}

./parse_primary : Bool -> (BP Expr)
| inline = do BP {
  let starts_with_num = do BP {
    ask x = (BP/peek_one)
    ret (Maybe/fold _ x Bool #False BP/is_num_char)
  }
  let starts_with_lam = do BP {
    ask is_lam = (BP/starts_with "λ")
    ask is_kw  = (BP/starts_with_keyword "lambda")
    ret (Bool/or is_lam is_kw)
  }
  let starts_with_nam = do BP {
    ask x = (BP/peek_one)
    ret (Maybe/fold _ x Bool #False BP/is_name_char)
  }

  ask (BP/skp_inline inline)
  (BP/first_with_guard _ [
    ((BP/starts_with "*") , (./parse_era))
    ((BP/starts_with "$") , (./parse_lnk))
    ((BP/starts_with "'") , (./parse_chr))
    ((BP/starts_with "\""), (./parse_str))
    ((BP/starts_with "`") , (./parse_sym))
    ((BP/starts_with "[") , (./parse_lst))
    ((BP/starts_with "!["), (./parse_tree_node))
    ((BP/starts_with "!") , (./parse_tree_leaf inline))
    ((BP/starts_with "(") , (./parse_parens))
    (starts_with_lam          , (./parse_lam inline))
    (starts_with_num          , (./parse_num))
    (starts_with_nam          , (./parse_var))
  ] (BP/fail _ "Expected expression"))
}

./parse_era : (BP Expr)
= do BP {
  ask (BP/consume "*")
  ret #Era
}

./parse_lnk : (BP Expr)
= do BP {
  ask (BP/consume "$")
  ask nam = (BP/parse_var_name)
  ret #Lnk{nam}
}

./parse_chr : (BP Expr)
= do BP {
  ask val = (BP/parse_quoted_char)
  ret #Num{#U24{val}}
}

./parse_str : (BP Expr)
= do BP {
  ask val = (BP/parse_quoted_string)
  ret #Str{val}
}

./parse_sym : (BP Expr)
| = do BP {
  ask _   = (BP/consume "`")
  ask sym = (./parse_sym/go 4 0)
  ret #Num{#U24{sym}}
}
./parse_sym/go : U64 -> U64 -> (BP U64)
| 1+n acc = do BP {
  ask c = (BP/advance_one)
  match c {
    #Some{c}:
      if (Bool/and (Char/gte c 'A') (Char/lte c 'Z')) {
        (./parse_sym/go n (+ (<< acc 6) (- c 'A')))
      } else if (Bool/and (Char/gte c 'a') (Char/lte c 'z')) {
        (./parse_sym/go n (+ (<< acc 6) (+ (- c 'A') 26)))
      } else if (Bool/and (Char/gte c '0') (Char/lte c '9')) {
        (./parse_sym/go n (+ (<< acc 6) (+ (- c '0') 52)))
      } else if (Char/eq c '+') {
        (./parse_sym/go n (+ (<< acc 6) 62))
      } else if (Char/eq c '/')  {
        (./parse_sym/go n (+ (<< acc 6) 63))
      } else if (Char/eq c '`') {
        (BP/pure _ acc)
      } else {
        (BP/fail _ (String/concat ["Invalid symbol character '" (String/from_char c) "'"]))
      }
    #None: (BP/fail _ "Expected symbol")
  }
}
| 0   acc = do BP {
  ask (BP/consume "`")
  ret acc
}

./parse_lst : (BP Expr)
| = do BP {
  ask els = (BP/list_like _ (BP/Imp/parse_expr #False #False) "[" "]" "," #True #False 0)
  ret #Lst{els}
}

./parse_tree_node : (BP Expr)
| = do BP {
  ask (BP/consume "![")
  ask (BP/skp)
  ask lft = (BP/Imp/parse_expr #False #False)
  ask (BP/skp)
  ask (BP/consume ",")
  ask (BP/skp)
  ask rgt = (BP/Imp/parse_expr #False #False)
  ask (BP/skp)
  ask (BP/consume "]")
  ret #TreNod{lft rgt}
}

./parse_tree_leaf : Bool -> (BP Expr)
| inline = do BP {
  ask (BP/consume "!")
  ask (BP/skp)
  ask val = (./parse_single inline)
  ret #TreLea{val}
}

./parse_parens : (BP Expr)
| = do BP {
  ask (BP/consume "(")
  ask (BP/skp)
  ask val = (BP/Imp/parse_expr #False #True)
  ask (BP/skp)
  ask (BP/consume ")")
  ret val
}

./parse_lam : Bool -> (BP Expr)
| inline = do BP {
  let p_var  = do BP {
    ask uns = (BP/try_consume "$")
    ask _   = (BP/skp_inline inline)
    ask nam = (BP/parse_var_name)
    ret (nam, uns) :: (_ & _)
  }

  ask lam = (BP/try_consume "λ")
  ask if lam {
    (BP/pure _ 0)
  } else do BP {
    ask (BP/parse_keyword "lambda")
    (BP/pure _ 0)
  }
  ask (BP/skp_inline inline)
  ask nam = (BP/list_like _ p_var "" ":" "," #True inline 0)
  ask (BP/skp_inline inline)
  ask bod = (BP/Imp/parse_expr inline #True)
  ret #Lam{nam bod}
}

./parse_num : (BP Expr)
| = do BP {
  ask num = (BP/parse_number)
  ret #Num{num}
}

./parse_var : (BP Expr)
| = do BP {
  ask nam = (BP/parse_var_name)
  ret #Var{nam}
}

./parse_postfix : Expr -> Bool -> (BP Expr)
| term inline = do BP {
  ask (BP/skp_inline inline)
  (BP/first_with_guard _ [
    ((BP/starts_with "("), (./parse_call term inline))
    ((BP/starts_with "{"), (./parse_ctr term inline))
    ((BP/starts_with "["), (./parse_tree_get term inline))
  ] (BP/pure _ term))
}

./parse_call : Expr -> Bool -> (BP Expr)
| term inline = do BP {
  ask args = (BP/list_like _ (./parse_call_arg) "(" ")" "," #True #False 0)
  // TODO: Check if args and kwargs are in the right order
  let (arg, kwarg) = (List/foldr _ args ((List _) & (List _)) ([], []) λp λacc
    let (nam, val) = p
    let (arg, kwarg) = acc
    match nam {
      #Some{nam}: (arg         , (((nam, val) :: (_&_)) ;; kwarg))
      #None     : ((val ;; arg), kwarg)
    })
  (./parse_postfix #Cal{term arg kwarg} inline)
}

./parse_call_arg : (BP ((Maybe String) & Expr))
| = do BP {
  ask arg = (BP/Imp/parse_expr #False #False)
  match arg {
    #Var{nam}: do BP {
      ask (BP/skp)
      ask kwarg = (BP/try_consume "=")
      if kwarg do BP {
        ask (BP/skp)
        ask val = (BP/Imp/parse_expr #False #False)
        (BP/pure _ (#Some{nam}, val))
      } else {
        (BP/pure _ (#None, arg))
      }
    }
    _: (BP/pure _ (#None, arg))
  }
}

./parse_ctr : Expr -> Bool -> (BP Expr)
| #Var{nam} inline = do BP {
  ask args = (BP/list_like _ (./parse_call_arg) "{" "}" "," #True #False 0)
  // TODO: Check if args and kwargs are in the right order
  let (arg, kwarg) = (List/foldr _ args ((List _) & (List (_&_))) ([], []) λp λacc
    let (nam, val) = p
    let (arg, kwarg) = acc
    match nam {
      #Some{nam}: (arg         , ((nam, val) ;; kwarg))
      #None     : ((val ;; arg), kwarg)
    })
  (./parse_postfix #Ctr{nam arg kwarg} inline)
}
| term _ = (BP/fail _ "Expected constructor name")

./parse_tree_get : Expr -> Bool -> (BP Expr)
| term inline = do BP {
  ask (BP/consume "[")
  ask BP/skp
  ask key = (BP/Imp/parse_expr #False #False)
  ask BP/skp
  ask (BP/consume "]")
  (./parse_postfix #MapGet{term key} inline)
}


#test : (BP/run Expr (. #True #True) "*")                  == #Done{"", #Era}
#test : (BP/run Expr (. #True #True) "(*)")                == #Done{"", #Era}
#test : (BP/run Expr (. #True #True) "a")                  == #Done{"", #Var{"a"}}
#test : (BP/run Expr (. #True #True) "a()")                == #Done{"", #Cal{#Var{"a"} [] []}}
#test : (BP/run Expr (. #True #True) "a(b, c=d)")          == #Done{"", #Cal{#Var{"a"} [#Var{"b"}] [("c", #Var{"d"})]}}
#test : (BP/run Expr (. #True #True) "a{b, c=d}")          == #Done{"", #Ctr{"a"       [#Var{"b"}] [("c", #Var{"d"})]}}
#test : (BP/run Expr (. #True #True) "lambda a,$b,: a,$b") == #Done{"", #Lam{[("a",#False) ("b",#True)] #Tup{[#Var{"a"} #Lnk{"b"}]}}}
#test : (BP/run Expr (. #True #True) "![!a, !b]")          == #Done{"", #TreNod{#TreLea{#Var{"a"}} #TreLea{#Var{"b"}}}}
#test : (BP/run Expr (. #True #True) "1*2 + 3*4 + 5*6")    == #Done{"", #Opr{#Add #Opr{#Add #Opr{#Mul #Num{#U24{1}} #Num{#U24{2}}} #Opr{#Mul #Num{#U24{3}} #Num{#U24{4}}}} #Opr{#Mul #Num{#U24{5}} #Num{#U24{6}}}}}
