use Bend/Fun/FnDef as FnDef
use Bend/Fun/MatchRule as MatchRule
use Bend/Fun/Term as Term
use Bend/Fun/Pattern as Pat

Term/to-string : Term -> String
| #Lam{pat bod}     = (String/concat ["λ" (Pat/to-string pat) " " (Term/to-string bod)])
| #Var{nam}         = nam
| #Lnk{nam}         = (String/concat ["$" nam])
| #Let{pat val nxt} = (String/concat ["let " (Pat/to-string pat) " = " (Term/to-string val) "; " (Term/to-string nxt)])
| #Wth{typ bod}     = (String/concat ["with " typ " { " (Term/to-string bod) " }"])
| #Ask{pat val nxt} = (String/concat ["ask " (Pat/to-string pat) " = " (Term/to-string val) "; " (Term/to-string nxt)])
| #Use{nam val nxt} = (String/concat ["use " (Bend/Fun/bnd-to-string nam) " = " (Term/to-string val) "; " (Term/to-string nxt)])
| #App{fun arg}     = (String/concat ["(" (show-app #App{fun arg}) ")"])
| #Fan{#Tup els}    = (String/concat ["(" (String/join ", " (List/map _ _ Term/to-string els)) ")"])
| #Fan{#Dup els}    = (String/concat ["{" (String/join " " (List/map _ _ Term/to-string els)) "}"])
| #Num{val}         = (Bend/Fun/Num/to-string val)
| #Str{val}         = (String/concat ["\"" val "\""])
| #Lst{els}         = (String/concat ["[" (String/join ", " (List/map _ _ Term/to-string els)) "]"])
| #Opr{opr fst snd} = (String/concat ["(" (Bend/Fun/Op/to-string opr) " " (Term/to-string fst) " " (Term/to-string snd) ")"])
| #Mat{bnd arg with_bnd with_arg arms} =
  (String/concat ["match " (Bend/Fun/bnd-to-string bnd) " = " (Term/to-string arg) " " (show-with with_bnd with_arg) "{ " (String/join "; " (List/map _ _ MatchRule/to-string arms)) " }"])
| #Swt{bnd arg with_bnd with_arg pred arms} = (String/concat [
  "switch " (Bend/Fun/bnd-to-string bnd) " = " (Term/to-string arg) " " (show-with with_bnd with_arg) "{ " (show-swt-arms 0 pred arms) " }"])
| #Fld{bnd arg with_bnd with_arg arms} = (String/concat [
  "fold " (Bend/Fun/bnd-to-string bnd) " = " (Term/to-string arg) " " (show-with with_bnd with_arg) "{ " (String/join "; " (List/map _ _ MatchRule/to-string arms)) " }"])
| #Bnd{bnd arg cond step base} = (String/concat [
  "bend " (String/join ", " (List/map _ _ show-bnd-init (List/zip _ _ bnd arg))) " { " "when " (Term/to-string cond) ": " (Term/to-string step) "; " "else: " (Term/to-string base) " }"])
| #Opn{typ var bnd bod} = (String/concat [
  "open " typ " " (Bend/Fun/bnd-to-string var) (String/join "" (List/map _ _ λx (String/concat [" " (Bend/Fun/bnd-to-string x)]) bnd)) "; " (Term/to-string bod)])
| #Ref{nam} = nam
| #Def{def nxt} = (String/concat ["def " (FnDef/to-string def) "\n" (Term/to-string nxt)])
| #Era = "*"

show-with : (List (Maybe String)) -> (List Term) -> String
| [] [] = ""
| with_bnd with_arg =
  (String/concat ["with " (String/join ", " (List/map _ _ show-bnd-arg (List/zip _ _ with_bnd with_arg))) " "])

show-bnd-arg : (Pair (Maybe String) Term) -> String
| #Pair{bnd arg} = (String/concat [(Bend/Fun/bnd-to-string bnd) " = " (Term/to-string arg)])

show-app : Term -> String
| #App{fun arg} = (String/concat [(show-app fun) " " (Term/to-string arg)])
| term          = (Term/to-string term)

show-swt-arms : U64 -> (Maybe String) -> (List Term) -> String
| n pred []              = ""
| n pred [arm]           = (String/concat ["_ " (Bend/Fun/bnd-to-string pred) ": " (Term/to-string arm)])
| n pred #Cons{arm arms} = (String/concat [(U64/to-string n) ": " (Term/to-string arm) "; " (show-swt-arms (+ n 1) pred arms)])

show-bnd-init : (Pair (Maybe String) Term) -> String
| #Pair{fst snd} = (String/concat [(Bend/Fun/bnd-to-string fst) " = " (Term/to-string snd)])
