// Pads a string with a character to the left until the given length is reached.
// - n: Desired length of the resulting string.
// - c: Character to use for padding.
// - s: Input string to pad.
// = New string padded to length n with character c on the left side.
String/pad-left : Nat -> U32 -> String -> String
| n c s =
  let #Pair{str len} = (List/length-got U32 s)
  if (Nat/lt len n) {
    (String/append (String/replicate (Nat/sub n len) c) str)
  } else {
    str
  }

#test: (String/pad-left #3 'x' "") == "xxx"
#test: (String/pad-left #5 'y' "123") == "yy123"
#test: (String/pad-left #2 'z' "hello") == "hello"
#test: (String/pad-left #5 ' ' "abc") == "  abc"
